import Pseudorandom.SD
import Mathlib.Tactic.Rify
import Mathlib.RingTheory.RootsOfUnity.Complex
import Mathlib.Analysis.SpecialFunctions.Log.Base
import Mathlib.Algebra.Order.Chebyshev
import LeanAPAP.Prereqs.Discrete.DFT.Compact
import LeanAPAP.Prereqs.Expect.Basic
import Mathlib.NumberTheory.Harmonic.Bounds
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Bounds

open Classical Real Finset BigOps

variable
   {Œ± : Type*} [Œ±nonempty: Nonempty Œ±] [Fintype Œ±] [AddCommGroup Œ±]
   {Œ≤ : Type*} [Nonempty Œ≤] [Fintype Œ≤] [AddCommGroup Œ≤]
   [RCLike ùïú]
   (a b : Œ± ‚Üí ùïú)

open scoped NNReal

theorem l1Norm_le_sqrt_card_mul_l2Norm :
  ‚Äña‚Äñ_[1] ‚â§ Real.sqrt (Fintype.card Œ±) * ‚Äña‚Äñ_[2] := calc
    ‚Äña‚Äñ_[1] = ‚Äñ1 * a‚Äñ_[1] := by simp
    _ ‚â§ ‚Äñ1‚Äñ_[(2 : NNReal)] * ‚Äña‚Äñ_[2] := by
      apply l1Norm_mul_le
      rw [NNReal.isConjExponent_iff_eq_conjExponent]
      rw [NNReal.sub_def]
      norm_num
      norm_num
    _ = Real.sqrt (Fintype.card Œ±) * ‚Äña‚Äñ_[2] := by
      congr
      rw [lpNorm_one (p := 2), Real.sqrt_eq_rpow]
      norm_num
      norm_num

lemma lpNorm_eq_card_rpow_mul_nlpNorm (p : NNReal) (hp : p ‚â† 0) :
    ‚Äña‚Äñ_[p] = (Fintype.card Œ±)^(p.toReal‚Åª¬π) * ‚Äña‚Äñ‚Çô_[p] := calc
  ‚Äña‚Äñ_[p] = (‚àë i, ‚Äña i‚Äñ ^ (p.toReal)) ^ (p.toReal‚Åª¬π) := lpNorm_eq_sum hp ..
  _ = (Fintype.card Œ± * ùîº i, ‚Äña i‚Äñ ^ (p.toReal)) ^ (p.toReal‚Åª¬π) := by
    simp only [Fintype.card_mul_expect]
  _ = (Fintype.card Œ±)^(p.toReal‚Åª¬π) * (ùîº i, ‚Äña i‚Äñ ^ (p.toReal)) ^ (p.toReal‚Åª¬π) := by
    rw [mul_rpow]
    simp
    apply expect_nonneg
    intros
    apply rpow_nonneg
    simp
  _ = (Fintype.card Œ±)^(p.toReal‚Åª¬π) * ‚Äña‚Äñ‚Çô_[p] := by
    rw [nlpNorm_eq_expect hp]

lemma lpNorm_le_card_rpow_mul_linftyNorm (p : NNReal) (hp : p ‚â† 0) :
    ‚Äña‚Äñ_[p] ‚â§ (Fintype.card Œ±)^(p.toReal‚Åª¬π) * ‚Äña‚Äñ_[‚ä§] := calc
  ‚Äña‚Äñ_[p] = (‚àë i, ‚Äña i‚Äñ ^ (p.toReal)) ^ (p.toReal‚Åª¬π) := lpNorm_eq_sum hp ..
  _ ‚â§ (‚àë __, ‚Äña‚Äñ_[‚ä§] ^ (p.toReal)) ^ (p.toReal‚Åª¬π) := by
    gcongr with i
    rw [linftyNorm_eq_ciSup]
    apply le_ciSup (c := i)
    simp [Set.Finite.bddAbove, Set.finite_range]
  _ = (Fintype.card Œ± * ‚Äña‚Äñ_[‚ä§] ^ (p.toReal)) ^ (p.toReal‚Åª¬π) := by
    simp; rfl
  _ ‚â§ (Fintype.card Œ±)^(p.toReal‚Åª¬π) * (‚Äña‚Äñ_[‚ä§] ^ (p.toReal)) ^ (p.toReal‚Åª¬π) := by
    rw [mul_rpow]
    simp
    apply rpow_nonneg
    simp
  _ = (Fintype.card Œ±)^(p.toReal‚Åª¬π) * ‚Äña‚Äñ_[‚ä§] := by
    congr
    rw [‚Üê rpow_mul]
    field_simp
    simp

lemma l2Inner_le_l1Norm_mul_linftyNorm :
    ‚Äñ‚ü™a, b‚ü´_[ùïú]‚Äñ ‚â§ ‚Äña‚Äñ_[1] * ‚Äñb‚Äñ_[‚ä§] := by
  rw [l2Inner, l1Norm_eq_sum, sum_mul]
  refine (norm_sum_le ..).trans ?_
  apply sum_le_sum
  intro i _
  simp only [norm_mul, RingHomIsometric.is_iso]
  gcongr
  rw [linftyNorm_eq_ciSup]
  apply le_ciSup (c := i)
  simp [Set.Finite.bddAbove, Set.finite_range]

lemma l2Norm_le_sqrt_l1Norm_mul_linftyNorm (a : Œ± ‚Üí ‚Ñù) :
    ‚Äña‚Äñ_[2] ‚â§ Real.sqrt (‚Äña‚Äñ_[1] * ‚Äña‚Äñ_[‚ä§]) := by
  rw [Real.le_sqrt]
  convert_to ‚ü™a, a‚ü´_[‚Ñù] ‚â§ ‚Äña‚Äñ_[1] * ‚Äña‚Äñ_[‚ä§]
  simp
  convert l2Inner_le_l1Norm_mul_linftyNorm a a
  simp
  simp
  apply mul_nonneg <;> simp

variable
   (a b : Œ± ‚Üí ‚Ñù)

theorem L1_le_card_rpow_mul_dft_norm :
    ‚Äña‚Äñ_[1] ‚â§ ((Fintype.card Œ±)^(3/2 : ‚Ñù) : ‚Ñù) * ‚Äñcft (a ¬∑)‚Äñ_[‚ä§] :=
  calc
    ‚Äña‚Äñ_[1] ‚â§ Real.sqrt (Fintype.card Œ±) * ‚Äña‚Äñ_[(2 : NNReal)] := l1Norm_le_sqrt_card_mul_l2Norm ..
    _ = (Fintype.card Œ±) * ‚Äña‚Äñ‚Çô_[2] := by
      rw [lpNorm_eq_card_rpow_mul_nlpNorm]
      rw [‚Üê mul_assoc]
      congr
      rw [Real.sqrt_eq_rpow, ‚Üê rpow_add]
      norm_num
      simp [Fintype.card_pos]
      norm_num
    _ = (Fintype.card Œ±) * ‚Äñcft (a ¬∑)‚Äñ_[2] := by
      congr
      rw [l2Norm_cft, nlpNorm_eq_expect', nlpNorm_eq_expect']
      congr
      ext
      simp
      simp
      simp
    _ ‚â§ (Fintype.card Œ±) * (Real.sqrt (Fintype.card Œ±) * ‚Äñcft (a ¬∑)‚Äñ_[‚ä§]) := by
      gcongr
      rw [Real.sqrt_eq_rpow]
      convert lpNorm_le_card_rpow_mul_linftyNorm (cft (a ¬∑)) 2 (by norm_num) using 3
      simp
      simp
    _ = ((Fintype.card Œ±)^(3/2 : ‚Ñù) : ‚Ñù) * ‚Äñcft (a ¬∑)‚Äñ_[‚ä§] := by
      rw [sqrt_eq_rpow, ‚Üê mul_assoc, ‚Üê rpow_one_add']
      congr 1
      norm_num
      simp
      norm_num

lemma lemma43 (t Œµ : NNReal)
    (h : ‚àÄ œá : AddChar Œ± ‚ÑÇ, (AddChar.IsNontrivial œá) ‚Üí ‚Äñcft (a ¬∑) œá‚Äñ ‚â§ Œµ / (Fintype.card Œ±))
    (œÉ : Œ± ‚Üí Œ≤) (h‚ÇÇ : ‚àÄ œá : AddChar Œ≤ ‚ÑÇ,
      ‚Äñcft (œá ‚àò œÉ)‚Äñ_[1] ‚â§ t
    ):
    ‚ÄñœÉ # a - œÉ # (Function.const Œ± (ùîº x, a x))‚Äñ_[1] ‚â§ t * Œµ * Real.sqrt (Fintype.card Œ≤)
  := by
  suffices ‚Äñcft (fun x => (œÉ # a - œÉ # (Function.const Œ± (ùîº x, a x))) x)‚Äñ_[‚ä§] ‚â§ t * Œµ / (Fintype.card Œ≤) by
    calc ‚ÄñœÉ # a - œÉ # (Function.const Œ± (ùîº x, a x))‚Äñ_[1]
      _ ‚â§ (Fintype.card Œ≤)^(3/2 : ‚Ñù) * ‚Äñcft (fun x => (œÉ # a - œÉ # (Function.const Œ± (ùîº x, a x))) x)‚Äñ_[‚ä§] := L1_le_card_rpow_mul_dft_norm _
      _ ‚â§ (Fintype.card Œ≤)^(3/2 : ‚Ñù) * (t * Œµ / (Fintype.card Œ≤)) := by gcongr
      _ = t * Œµ * ((Fintype.card Œ≤)^(3/2 : ‚Ñù) / (Fintype.card Œ≤)) := by ring
      _ = t * Œµ * Real.sqrt (Fintype.card Œ≤) := by
        rw [sqrt_eq_rpow, ‚Üê rpow_sub_one]
        norm_num
        simp
  rw [linftyNorm_eq_ciSup]
  apply ciSup_le
  intro œá
  dsimp only [cft_apply, nl2Inner_eq_expect]
  simp_rw [‚Üê transfer_sub]
  change ‚Äñexpect _ fun i => _ * (Complex.ofReal ‚àò _) i‚Äñ ‚â§ _
  simp_rw [comp_transfer]
  conv =>
    lhs
    rhs
    rhs
    intro
    rw [mul_comm]
  rw [transfer_expect]
  simp_rw [mul_comm]
  rw [‚Üê nl2Inner_eq_expect]
  by_cases he : œá = 0
  ¬∑ simp only [he, AddChar.one_apply, Function.comp_apply, Pi.sub_apply, map_sub,
      Complex.ofReal_eq_coe, map_div‚ÇÄ, map_sum, map_natCast, Complex.norm_eq_abs]
    change Complex.abs (_ ‚Ä¢ nl2Inner (Function.const Œ± 1) _) ‚â§ _
    rw [nl2Inner_const_left]
    simp [expect_sub_distrib]
    positivity
  ¬∑ calc ‚Äñ(Fintype.card Œ± / Fintype.card Œ≤ : NNRat) ‚Ä¢ nl2Inner (œá ‚àò œÉ) (‚áëComplex.ofReal ‚àò (a - Function.const Œ± (expect univ fun x => a x)))‚Äñ
      _ = (Fintype.card Œ± / Fintype.card Œ≤ : ‚Ñù) * ‚Äñnl2Inner (œá ‚àò œÉ) (‚áëComplex.ofReal ‚àò (a - Function.const Œ± (expect univ fun x => a x)))‚Äñ := by
        rw [‚Üê nnratCast_smul_eq_nnqsmul ‚Ñù]
        simp
      _ = (Fintype.card Œ± / Fintype.card Œ≤ : ‚Ñù) * ‚Äñl2Inner (cft (œá ‚àò œÉ)) (cft (‚áëComplex.ofReal ‚àò (a - Function.const Œ± (expect univ fun x => a x))))‚Äñ := by
        rw [l2Inner_cft]
      _ ‚â§ (Fintype.card Œ± / Fintype.card Œ≤ : ‚Ñù) * (‚Äñcft (œá ‚àò œÉ)‚Äñ_[1] * ‚Äñcft (‚áëComplex.ofReal ‚àò (a - Function.const Œ± (expect univ fun x => a x)))‚Äñ_[‚ä§]) := by
        gcongr
        apply l2Inner_le_l1Norm_mul_linftyNorm
      _ ‚â§ (Fintype.card Œ± / Fintype.card Œ≤ : ‚Ñù) * (t * (Œµ / (Fintype.card Œ±))) := by
        gcongr
        apply h‚ÇÇ
        rw [linftyNorm_eq_ciSup]
        apply ciSup_le
        intro œà
        by_cases hœà : œà = 0
        ¬∑ simp only [map_comp_sub, Function.comp_const, Complex.ofReal_eq_coe,
          Complex.ofReal_expect, hœà, cft_apply, AddChar.coe_zero, Complex.norm_eq_abs]
          change Complex.abs (nl2Inner (Function.const Œ± 1) _) ‚â§ _
          rw [nl2Inner_const_left]
          simp [expect_sub_distrib]
          positivity
        ¬∑ simp only [map_comp_sub, Function.comp_const, Complex.ofReal_eq_coe,
          Complex.ofReal_expect, cft_sub, Pi.sub_apply]
          rw [cft_const]
          simp only [sub_zero]
          apply h
          exact (AddChar.isNontrivial_iff_ne_trivial _).mpr hœà
          exact hœà
      _ = t * Œµ / (Fintype.card Œ≤) := by
        field_simp
        ring_nf


variable (n m : ‚Ñï+) (h‚Çò : m ‚â§ n)

local notation "Œ±" => ZMod n
local notation "Œ≤" => ZMod m
def lemma44C : ‚Ñù := 1

lemma range_eq_zmod_image : range ‚Üën = image (fun t => ZMod.val t) (univ : Finset Œ±) := by
  ext x
  simp only [mem_range, mem_image, mem_univ, true_and]
  constructor
  intro v
  exists x
  simp only [ZMod.val_nat_cast]
  apply Nat.mod_eq_of_lt v
  rintro ‚ü®a, ha‚ü©
  rw [‚Üê ha]
  apply ZMod.val_lt

lemma le_add_div_add_of_le_of_le (a b n : ‚Ñù) (hb : 0 < b) (hn : 0 < n)
    (h : a/b ‚â§ n) : a/b ‚â§ (a + 1)/(b + 1/n) := by
  rw [div_le_div_iff]
  rw [div_le_iff] at h
  ring_nf
  gcongr
  rwa [mul_inv_le_iff]
  exact hn
  exact hb
  exact hb
  positivity

lemma circle_lower_bound (x : ‚Ñù) :
  2 - |4 * x - 2| ‚â§ ‚Äñ(Circle.e x : ‚ÑÇ) - 1‚Äñ := by
  simp only [Circle.coe_e, Complex.ofReal_ofNat, Complex.norm_eq_abs]
  rw [Complex.exp_mul_I, Complex.abs_eq_sqrt_sq_add_sq]
  conv =>
    rhs
    rhs
    congr
    ¬∑ lhs
      simp [-Complex.ofReal_mul, Complex.cos_ofReal_re]
    ¬∑ simp [-Complex.ofReal_mul, Complex.sin_ofReal_re]
  conv =>
    rhs
    rhs
    tactic =>
      change _ = 2 - 2 * cos (2 * œÄ * x)
      rw [Real.sin_sq]
      ring_nf
  wlog h : (0 ‚â§ x ‚àß x ‚â§ 1/2) generalizing x
  ¬∑ simp only [not_and_or, not_le] at h
    cases h
    ¬∑ calc
        2 - |4 * x - 2| ‚â§ 0 := by
          rw [abs_of_nonpos]
          linarith
          linarith
        _ ‚â§ Real.sqrt (2 - 2 * cos (2 * œÄ * x)) := by positivity
    by_cases x ‚â§ 1
    ¬∑ convert this (1 - x) (by constructor <;> linarith) using 2
      ¬∑ rw [abs_eq_abs]
        right
        ring_nf
      ¬∑ congr 2
        ring_nf
        conv =>
          rhs
          rw [mul_comm]
        simp
    ¬∑ calc
        2 - |4 * x - 2| ‚â§ 0 := by
          rw [abs_of_nonneg]
          linarith
          linarith
        _ ‚â§ Real.sqrt (2 - 2 * cos (2 * œÄ * x)) := by positivity
  have ‚ü®h1, h2‚ü© := h
  rw [‚Üê ge_iff_le]
  calc Real.sqrt (2 - 2 * cos (2 * œÄ * x))
    _ ‚â• Real.sqrt (2 - 2 * (1 - 2 / œÄ^2 * (2 * œÄ * x)^2)) := by
      apply Real.sqrt_le_sqrt
      gcongr
      apply Real.cos_quadratic_upper_bound
      rw [abs_of_nonneg]
      rw [mul_comm, ‚Üê mul_assoc]
      apply mul_le_of_le_one_left
      positivity
      linarith
      positivity
    _ = Real.sqrt ((4 * x)^2) := by
      congr
      field_simp
      ring_nf
    _ = 4 * x := by apply Real.sqrt_sq; linarith
    _ = 2 - |4 * x - 2| := by
      rw [abs_of_nonpos]
      ring_nf
      linarith

set_option maxHeartbeats 500000

theorem lemma44 (œá : AddChar Œ≤ ‚ÑÇ) : ‚Äñcft (œá ‚àò (fun x : Œ± => (x.val : Œ≤)))‚Äñ_[1] ‚â§ 6 * Real.log n + 6 := by
  simp_rw [l1Norm_eq_sum, cft_apply, nl2Inner, expect]
  simp only [Function.comp_apply, ‚Üê nnratCast_smul_eq_nnqsmul ‚ÑÇ, NNRat.cast_inv, NNRat.cast_natCast,
    smul_eq_mul, norm_mul, norm_inv, Complex.norm_nat]
  simp_rw [‚Üê AddChar.map_neg_eq_conj, ‚Üê mul_sum]
  let w := (AddChar.zmodAddEquiv (n := m) (by simp)).symm œá
  have (y) : œá y = (AddChar.zmod m w) y := by
    have : œá = AddChar.zmodAddEquiv (n := m) (by simp) w := by unfold_let w; simp
    simp [this]
    rfl
  simp_rw [this]
  rw [‚Üê Equiv.sum_comp (Œπ := Œ±) (Œ∫ := AddChar Œ± ‚ÑÇ) (AddChar.zmodAddEquiv (n := n) (by simp))]
  conv =>
    lhs
    rhs
    rhs
    intro t
    rhs
    rhs
    intro x
    tactic =>
      simp only [EquivLike.coe_coe, AddChar.zmodAddEquiv_apply]
      change ((AddChar.zmod n t) (-x) * (AddChar.zmod m w) (x.val) : circle) = (_ : ‚ÑÇ)
      convert_to ((AddChar.zmod n (t.val : ‚Ñ§)) (- x.val : ‚Ñ§) * (AddChar.zmod m (w.val : ‚Ñ§)) (x.val : ‚Ñ§) : circle) = (_ : ‚ÑÇ)
      congr <;> simp
      simp only [AddChar.zmod_apply]
      simp only [ZMod.nat_cast_val, ZMod.int_cast_cast, Int.cast_neg, mul_neg, ‚Üê
        AddChar.map_add_mul]
      convert_to Circle.e (x.val * (w.val * n - t.val * m) / (n * m)) = (_ : ‚ÑÇ)
      congr
      simp only [ZMod.nat_cast_val]
      field_simp
      ring
      rfl
  calc (univ.card : ‚Ñù)‚Åª¬π * ‚àë t : Œ±, ‚Äñ‚àë x : Œ±, (Circle.e (x.val * (w.val * n - t.val * m) / (n * m)) : ‚ÑÇ)‚Äñ
    _ = (n : ‚Ñù)‚Åª¬π * ‚àë t : Œ±, ‚Äñ‚àë x ‚àà Finset.range n,
        (Circle.e (x * (w.val * n - t.val * m) / (n * m)) : ‚ÑÇ)‚Äñ := by
      congr
      simp [card_univ]
      ext t
      congr 1
      apply Eq.symm
      convert Finset.sum_image ?_
      apply range_eq_zmod_image
      intro x _ y _ v
      apply ZMod.val_injective n v
    _ = (n : ‚Ñù)‚Åª¬π * ‚àë t : Œ±, ‚Äñ‚àë x ‚àà Finset.range n,
        (Circle.e ((w.val * n / m - t.val) / n) : ‚ÑÇ)^x‚Äñ := by
      congr with _
      congr with _
      rw [‚Üê SubmonoidClass.coe_pow, ‚Üê AddChar.map_nsmul_pow]
      congr 2
      field_simp
      ring_nf
    _ ‚â§ (n : ‚Ñù)‚Åª¬π * ‚àë t : Œ±,
        (‚Äñ(Circle.e ((w.val * n / m - t.val) / n) : ‚ÑÇ)^‚Üën - 1‚Äñ + 1) /
        (‚Äñ(Circle.e ((w.val * n / m - t.val) / n) : ‚ÑÇ) - 1‚Äñ + 1 / n) := by
      gcongr with t ht
      by_cases h : (Circle.e ((w.val * n / m - t.val) / n) : ‚ÑÇ) = 1
      ¬∑ rw [h]
        simp
      ¬∑ have := geom_sum_eq (x := (Circle.e ((w.val * n / m - t.val) / n) : ‚ÑÇ)) h n
        apply_fun (‚Äñ¬∑‚Äñ) at this
        rw [norm_div] at this
        rw [this]
        apply le_add_div_add_of_le_of_le
        simp only [Complex.norm_eq_abs, AbsoluteValue.pos_iff]
        exact fun v => h (eq_of_sub_eq_zero v)
        simp
        rw [‚Üê this]
        convert norm_sum_le ..
        convert_to ‚àë i ‚àà Finset.range n, (1 : ‚Ñù) = _
        simp
        apply sum_congr
        rfl
        intros
        simp [-Circle.coe_e]
    _ ‚â§ (n : ‚Ñù)‚Åª¬π * ‚àë t : Œ±,
        ((‚Äñ(Circle.e ((w.val * n / m - t.val) / n) : ‚ÑÇ)^‚Üën‚Äñ + ‚Äñ(1 : ‚ÑÇ)‚Äñ) + 1) /
        (‚Äñ(Circle.e ((w.val * n / m - t.val) / n) : ‚ÑÇ) - 1‚Äñ + 1 / n) := by
      gcongr
      apply norm_sub_le
    _ ‚â§ (n : ‚Ñù)‚Åª¬π * ‚àë t : Œ±,
        ((1 + (1 : ‚Ñù)) + 1) /
        (‚Äñ(Circle.e ((w.val * n / m - t.val) / n) : ‚ÑÇ) - 1‚Äñ + 1 / n) := by
      simp [-Circle.coe_e]
    _ = 3 * ‚àë t : Œ±,
        (n : ‚Ñù)‚Åª¬π /
        (‚Äñ(Circle.e ((w.val * n / m - t.val) / n) : ‚ÑÇ) - 1‚Äñ + 1 / n) := by
      rw [mul_sum, mul_sum]
      congr
      ext
      ring_nf
    _ = 3 * ‚àë t : Œ±,
        1 / (n * (‚Äñ(Circle.e ((w.val * n / m - t.val) / n) : ‚ÑÇ) - 1‚Äñ + 1 / n)) := by
      congr
      ext
      field_simp
    _ = 3 * ‚àë t : Œ±,
        1 / (n * (‚Äñ(Circle.e (Int.fract ((w.val * n / m - t.val) / n)) : ‚ÑÇ) - 1‚Äñ + 1 / n)) := by
      simp
    _ ‚â§ 3 * ‚àë t : Œ±,
        1 / (n * ((2 - |4 * Int.fract ((w.val * n / m - t.val) / n : ‚Ñù) - 2|) + 1 / n)) := by
      gcongr
      ¬∑ apply mul_pos
        simp
        apply add_pos_of_nonneg_of_pos
        apply sub_nonneg_of_le
        simp only [abs_sub_le_iff]
        constructor
        apply sub_left_le_of_le_add
        norm_num
        exact le_of_lt (Int.fract_lt_one _)
        apply sub_le_self
        apply mul_nonneg
        norm_num
        apply Int.fract_nonneg
        simp
      apply circle_lower_bound
    _ = 3 * ‚àë t : Œ±,
        1 / (2*n - |4 * (n * Int.fract ((w.val * n / m - t.val) / n : ‚Ñù)) - 2 * n| + 1) := by
      rcongr
      ring_nf
      conv =>
        lhs
        rhs
        rhs
        rw [‚Üê abs_of_nonneg (by simp : 0 ‚â§ (n : ‚Ñù)), ‚Üê abs_mul, abs_of_nonneg (by simp : 0 ‚â§ (n : ‚Ñù))]
      field_simp
      ring_nf
    _ = 3 * ‚àë t : Œ±,
        1 / (2*n - |4 * (n * Int.fract (((‚åäw.val * n / (m : ‚Ñù)‚åã + Int.fract (w.val * n / m : ‚Ñù)) - t.val) / n : ‚Ñù)) - 2 * n| + 1) := by simp
    _ = 3 * ‚àë t : Œ±,
        1 / (2*n - |4 * (n * Int.fract ((Int.fract (w.val * n / m : ‚Ñù) + (‚åäw.val * n / (m : ‚Ñù)‚åã - t.val)) / n : ‚Ñù)) - 2 * n| + 1) := by
      rcongr
      ring_nf
    _ = 3 * ‚àë t : Œ±,
        1 / (2*n - |4 * (n * Int.fract ((Int.fract (w.val * n / m : ‚Ñù) + (‚åäw.val * n / (m : ‚Ñù)‚åã - t).val) / n : ‚Ñù)) - 2 * n| + 1) := by
      congr with t
      congr 7
      rw [Int.fract_eq_fract]
      field_simp
      rw [‚Üê ZMod.nat_cast_val, ‚Üê ZMod.nat_cast_val, ‚Üê ZMod.nat_cast_val]
      norm_cast
      apply exists_eq_mul_left_of_dvd
      rw [‚Üê ZMod.int_cast_zmod_eq_zero_iff_dvd]
      simp
    _ = 3 * ‚àë t : Œ±,
        1 / (2*n - |4 * (n * Int.fract ((Int.fract (w.val * n / m : ‚Ñù) + t.val) / n : ‚Ñù)) - 2 * n| + 1) := by
      congr 1
      apply Fintype.sum_bijective (fun (x : Œ±) => (‚åäw.val * n / (m : ‚Ñù)‚åã - x))
      ¬∑ apply Function.Involutive.bijective
        intro x
        simp
      ¬∑ intro x
        rfl
    _ = 3 * ‚àë t : Œ±,
        1 / (2*n - |4 * (n * ((Int.fract (w.val * n / m : ‚Ñù) + t.val) / n : ‚Ñù)) - 2 * n| + 1) := by
      rcongr
      rw [Int.fract_eq_self]
      constructor
      ¬∑ apply div_nonneg
        apply add_nonneg
        simp
        norm_cast
        simp
        simp
      ¬∑ rw [div_lt_one, ‚Üê lt_sub_iff_add_lt]
        refine (Int.fract_lt_one _).trans_le ?_
        rw [le_sub_iff_add_le]
        norm_cast
        rw [Nat.one_add_le_iff]
        apply ZMod.val_lt
        simp
    _ = 3 * ‚àë t : Œ±,
        1 / (2*n - |4 * (Int.fract (w.val * n / m : ‚Ñù) + t.val) - 2 * n| + 1) := by
      rcongr
      field_simp
      ring_nf
    _ = 3 * ‚àë t ‚àà Finset.range n,
        1 / (2*n - |4 * (Int.fract (w.val * n / m : ‚Ñù) + t) - 2 * n| + 1) := by
      congr 1
      apply Eq.symm
      convert Finset.sum_image ?_
      apply range_eq_zmod_image
      intro x _ y _ v
      apply ZMod.val_injective n v
    _ = 3 * ‚àë t ‚àà Finset.range n,
        if t ‚â§ n/2 - Int.fract (w.val * n / m : ‚Ñù) then
          1 / (4 * (Int.fract (w.val * n / m : ‚Ñù) + t) + 1)
        else
          1 / (4 * (n - (Int.fract (w.val * n / m : ‚Ñù) + t)) + 1) := by
      rcongr t
      split
      ¬∑ rw [abs_of_nonpos]
        ring_nf
        linarith
      ¬∑ rw [abs_of_nonneg]
        ring_nf
        linarith
    _ ‚â§ 3 * ‚àë t ‚àà Finset.range n,
        if t ‚â§ n/2 - Int.fract (w.val * n / m : ‚Ñù) then
          1 / (4 * t + 1 : ‚Ñù)
        else
          1 / (4 * (n - (1 + t)) + 1 : ‚Ñù) := by
      gcongr with i hi
      split
      ¬∑ rw [one_div, one_div, inv_le_inv]
        linarith [Int.fract_nonneg (w.val * n / m : ‚Ñù)]
        linarith [Int.fract_nonneg (w.val * n / m : ‚Ñù)]
        positivity
      ¬∑ have : 1 ‚â§ (n : ‚Ñï) := by norm_cast; simp
        simp only [mem_range] at hi
        apply Nat.le_sub_one_of_lt at hi
        have : (i : ‚Ñù) ‚â§ (n : ‚Ñù) - 1 := by exact_mod_cast hi
        rw [one_div, one_div, inv_le_inv]
        linarith [Int.fract_lt_one (w.val * n / m : ‚Ñù)]
        linarith [Int.fract_lt_one (w.val * n / m : ‚Ñù)]
        linarith
    _ ‚â§ 3 * ‚àë t ‚àà Finset.range n,
        (1 / (4 * t + 1 : ‚Ñù) + 1 / (4 * (n - (1 + t)) + 1 : ‚Ñù)) := by
      gcongr with i hi
      have : 1 ‚â§ (n : ‚Ñï) := by norm_cast; simp
      simp only [mem_range] at hi
      apply Nat.le_sub_one_of_lt at hi
      have : (i : ‚Ñù) ‚â§ (n : ‚Ñù) - 1 := by exact_mod_cast hi
      split
      ¬∑ simp only [one_div, le_add_iff_nonneg_right, inv_nonneg]
        linarith
      ¬∑ simp only [one_div, le_add_iff_nonneg_left, inv_nonneg, ge_iff_le]
        linarith
    _ = 3 * (‚àë t ‚àà Finset.range n, 1 / (4 * t + 1 : ‚Ñù) + ‚àë t ‚àà Finset.range n, 1 / (4 * (n - (1 + t)) + 1 : ‚Ñù)) := by
      rw [sum_add_distrib]
    _ = 3 * (‚àë t ‚àà Finset.range n, 1 / (4 * t + 1 : ‚Ñù) + ‚àë t ‚àà Finset.range n, 1 / (4 * t + 1 : ‚Ñù)) := by
      congr 2
      convert Finset.sum_range_reflect ?_ n
      rename_i x hx
      congr 3
      simp only [mem_range] at hx
      have : 1+x ‚â§ (n : ‚Ñï) := Nat.one_add_le_iff.mpr hx
      norm_cast
      rw [Nat.sub_add_eq]
    _ = 6 * (‚àë t ‚àà Finset.range n, 1 / (4 * t + 1 : ‚Ñù)) := by rw [‚Üê two_mul]; ring
    _ ‚â§ 6 * ((‚àë t ‚àà Finset.range n, (t + 1 : ‚Ñö)‚Åª¬π : ‚Ñö) : ‚Ñù) := by
      push_cast
      simp only [one_div]
      gcongr
      linarith
    _ = 6 * (harmonic n : ‚Ñù) := by
      unfold harmonic
      norm_cast
    _ ‚â§ 6 * (1 + Real.log n) := by gcongr; apply harmonic_le_one_add_log
    _ = 6 * Real.log n + 6 := by ring_nf


-- theorem XOR_abelian (Œµ : ‚Ñù‚â•0)
--   (a : FinPMF Œ±) (h : ‚àÄ œá : AddChar Œ± ‚ÑÇ, (AddChar.IsNontrivial œá) ‚Üí ‚Äñdft (a ¬∑) œá‚Äñ ‚â§ Œµ) :
--   SD (a.apply fun x => (x.val : Œ≤)) (Uniform ‚ü®univ, univ_nonempty‚ü©) ‚â§
--     abelianC * (Œµ * Real.sqrt m * Real.log n + m / n) := by

--   sorry
