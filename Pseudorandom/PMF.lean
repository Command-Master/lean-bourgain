import Pseudorandom.Transfer
import LeanAPAP.Mathlib.Algebra.BigOperators.Basic
import Mathlib.Algebra.BigOperators.Fin
import Mathlib.Algebra.BigOperators.Order
import Mathlib.Data.Real.Basic
import LeanAPAP.Prereqs.Discrete.Convolution.Basic

open Finset BigOps

variable {Œ± : Type*} [Fintype Œ±] [DecidableEq Œ±]
        {Œ≤ : Type*} [Fintype Œ≤] [DecidableEq Œ≤]
        (a : FinPMF Œ±)

section basic

variable [Nonempty Œ±]

-- Definition of PMF over finite types
def FinPMF (Œ± : Type u) [Fintype Œ±] : Type u :=
  { f : Œ± ‚Üí ‚Ñù // ‚àë x, f x = 1 ‚àß ‚àÄ x, f x ‚â• 0}

instance instFunLike : FunLike (FinPMF Œ±) Œ± ‚Ñù where
  coe p := p.1
  coe_injective' _ _ h := Subtype.eq h

@[simp]
theorem FinPMF.sum_coe (p : FinPMF Œ±) : ‚àë a, p a = 1 := p.2.1


theorem FinPMF.val_apply (f : FinPMF Œ±) :
    f x = (f.val) x := rfl


theorem FinPMF.val_mk (f : Œ± ‚Üí ‚Ñù) {h : ‚àë x, f x = 1 ‚àß ‚àÄ x, f x ‚â• 0} :
    (‚ü®f, h‚ü© : FinPMF Œ±).val = f := rfl

@[simp]
theorem FinPMF.nonneg (p : FinPMF Œ±) : 0 ‚â§ p x := by simp [FinPMF.val_apply, p.2.2]

attribute [local simp] Set.Finite.bddAbove Set.finite_range card_univ

-- The uniform distribution over some nonempty set.
noncomputable def Uniform (t : { x : Finset Œ± // x.Nonempty }) : FinPMF Œ± := ‚ü®fun x : Œ± => if x ‚àà t.1 then (1 / (t.1.card) : ‚Ñù) else 0, by
  simp [sum_ite]
  have : t.1.card > 0 := Nonempty.card_pos t.2
  simp_all [Nat.pos_iff_ne_zero]
  intro x
  split <;> simp
‚ü©

noncomputable def Uniform_singleton (x : Œ±) : FinPMF Œ± := Uniform ‚ü®{x}, singleton_nonempty ..‚ü©

-- The value of the uniform distribution over the universe.
@[simp]
lemma uniform_single_value (x y : Œ±) : (Uniform_singleton x) y = if y = x then 1 else 0 := by
  simp [Uniform_singleton, Uniform, FinPMF.val_apply]

-- The value of the uniform distribution over the universe.
@[simp]
lemma Uniform.univ_uniform : (Uniform ‚ü®(univ : Finset Œ±), univ_nonempty‚ü©) x = 1/(Fintype.card Œ±) := by
  simp [Uniform, FinPMF.val_apply]

-- Multiplication of FinPMFs, treating them as independent.
instance instMulFinPMF : HMul (FinPMF Œ±) (FinPMF Œ≤) (FinPMF (Œ± √ó Œ≤)) where
  hMul := fun a b => ‚ü®fun x => (a x.1) * (b x.2), by
    constructor
    simp only [Fintype.sum_prod_type, ‚Üê sum_mul_sum, FinPMF.sum_coe, mul_one]
    intros
    apply mul_nonneg <;> simp
  ‚ü©

@[simp]
theorem FinPMF.mul_val (b : FinPMF Œ≤) : (a * b) (x, y) = (a x) * (b y) := rfl

end basic

section apply

-- Applying some function to a random variable.
noncomputable def FinPMF.apply (a : FinPMF Œ±) (f : Œ± ‚Üí Œ≤) : FinPMF Œ≤ :=
  ‚ü®f # a, by
    unfold transfer
    constructor
    simp
    rw [‚Üêsum_biUnion]
    have : Finset.biUnion univ (fun x => filter (fun y => f y = x) univ) = univ := by
      apply subset_antisymm
      ¬∑ simp
      ¬∑ aesop
    simp_all
    apply Set.pairwiseDisjoint_filter
    simp [sum_nonneg]
    ‚ü©

-- If B = g(A) then E[f(B)] = E[f(g(A))].
theorem apply_weighted_sum [RCLike ùïú] (g: Œ± ‚Üí Œ≤) (f : Œ≤ ‚Üí ùïú) : ‚àë x, ((a.apply g) x) * (f x) = ‚àë y, (a y) * (f (g y)) := by
  change ‚àë x, (RCLike.ofRealAm ‚àò (g # ‚Üëa)) x * f x = ‚àë x, (a x) * f (g x)
  simp_rw [comp_transfer]
  apply transfer_sum

lemma FinPMF.apply_equiv (f : Œ± ‚âÉ Œ≤) : (a.apply f) x = a (f.symm x) := by
  unfold apply transfer
  change ‚àë y ‚àà univ.filter (fun y => f y = x), a y = _
  convert_to ‚àë y ‚àà {f.symm x}, a y = _
  congr
  ext v
  constructor
  ¬∑ intro o
    simp only [mem_filter, mem_univ, true_and] at o
    rw [‚Üê o]
    simp
  ¬∑ intro o
    simp only [mem_singleton] at o
    simp [o]
  simp

lemma FinPMF.apply_swap (b : FinPMF Œ≤) : (a*b).apply Prod.swap = b*a := by
  apply Subtype.ext
  ext x
  have ‚ü®x1, x2‚ü© := x
  unfold apply transfer
  simp only [filter_congr_decidable]
  convert_to ‚àë x ‚àà univ.filter (fun x => x = (x2, x1)), (a*b) x = (b*a) (x1, x2)
  congr
  ext v
  constructor
  ¬∑ intro o
    apply_fun Prod.swap at o
    exact o
  ¬∑ intro o
    rw [o]
    rfl
  simp [filter_eq', mul_comm]

lemma FinPMF.apply_apply (f : Œ± ‚Üí Œ≤) (g : Œ≤ ‚Üí Œ≥) [Nonempty Œ≥] [Fintype Œ≥] [DecidableEq Œ≥] :
    (a.apply f).apply g = a.apply (g ‚àò f) := by
  apply Subtype.ext
  apply transfer_transfer

lemma FinPMF.eq_apply_id : a.apply id = a := by
  apply Subtype.ext
  apply transfer_id

end apply

section monoid

-- Subtraction of FinPMFs, treating them as independent.
noncomputable instance instSubFinPMF [Sub Œ±] : Sub (FinPMF Œ±) where
  sub := fun a b => (a*b).apply (fun x => x.1 - x.2)

-- Addition of FinPMFs, treating them as independent.
noncomputable instance instAddFinPMF [Add Œ±] : Add (FinPMF Œ±) where
  add := fun a b => (a*b).apply (fun x => x.1 + x.2)

-- Negation of FinPMF
noncomputable instance instNegFinPMF [Neg Œ±] : Neg (FinPMF Œ±) where
  neg := fun a => a.apply (fun x => -x)

-- Zero of FinPMF
noncomputable instance instZeroFinPMF [Zero Œ±] : Zero (FinPMF Œ±) where
  zero := ‚ü®fun x => if x = 0 then 1 else 0, by aesop‚ü©

@[simp]
lemma FinPMF.neg_apply [InvolutiveNeg Œ±] : (-a) x = a (-x) := calc
  (-a) x = ‚àë z ‚àà (univ.filter fun z => -z = x), a z := rfl
  _ = ‚àë z ‚àà {-x}, a z := by
    congr
    aesop
  _ = a (-x) := by simp

@[simp]
lemma FinPMF.neg_neg [InvolutiveNeg Œ±] : - -a = a := by
  apply Subtype.ext
  ext x
  change (- (- a)) x = a x
  rw [FinPMF.neg_apply, FinPMF.neg_apply]
  simp

lemma FinPMF.neg_add [AddCommGroup Œ±] : -(a + b) = -a + -b := by
  apply Subtype.ext
  ext x
  calc (-(a + b)) x
    _ = (a + b) (- x) := by simp
    _ = ‚àë y ‚àà univ.filter (fun (y : Œ± √ó Œ±) => y.1 + y.2 = -x), a y.1 * b y.2 := rfl
    _ = ‚àë y ‚àà univ.filter (fun (y : Œ± √ó Œ±) => y.1 + y.2 = x), a (-y.1) * b (-y.2) := by
      apply sum_bijective (fun x => (-x.1, -x.2))
      ¬∑ apply Function.Involutive.bijective
        intro x
        simp
      ¬∑ rintro ‚ü®i1, i2‚ü©
        simp only [mem_filter, mem_univ, true_and]
        constructor
        ¬∑ intro v
          apply_fun (- ¬∑) at v
          simp at v
          rw [‚Üê v]
          rw [add_comm]
        ¬∑ intro v
          apply_fun (- ¬∑) at v
          simp at v
          rw [‚Üê v]
          rw [add_comm]
      ¬∑ intros
        simp
    _ = ‚àë y ‚àà univ.filter (fun (y : Œ± √ó Œ±) => y.1 + y.2 = x), (-a) y.1 * (-b) y.2 := by simp
    _ = (-a + -b) x := rfl

lemma FinPMF.sub_eq_add_neg [AddGroup Œ±] : (a - b) = (a + -b) := by
  apply Subtype.ext
  ext x
  calc (a - b) x
    _ = ‚àë y ‚àà univ.filter (fun (y : Œ± √ó Œ±) => y.1 - y.2 = x), a y.1 * b y.2 := rfl
    _ = ‚àë y ‚àà univ.filter (fun (y : Œ± √ó Œ±) => y.1 + y.2 = x), a y.1 * b (-y.2) := by
      apply sum_bijective (fun x => (x.1, -x.2))
      ¬∑ apply Function.Involutive.bijective
        intro x
        simp
      ¬∑ rintro ‚ü®i1, i2‚ü©
        simp [_root_.sub_eq_add_neg]
      ¬∑ intros
        simp
    _ = ‚àë y ‚àà univ.filter (fun (y : Œ± √ó Œ±) => y.1 + y.2 = x), a y.1 * (-b) y.2 := by simp
    _ = (a + -b) x := rfl

noncomputable instance FinPMFCommMonoid [AddCommGroup Œ±] : AddCommMonoid (FinPMF Œ±) := {
  add := Add.add
  add_assoc := by
    intros a b c
    apply Subtype.ext
    ext x
    calc (a + b + c) x
      _ = (a ‚àó b ‚àó c) x := rfl
      _ = (a ‚àó (b ‚àó c)) x := by rw [conv_assoc]
      _ = (a + (b + c)) x := rfl
  add_comm := by
    intros a b
    apply Subtype.ext
    ext x
    calc (a + b) x
      _ = (a ‚àó b) x := rfl
      _ = (b ‚àó a) x := by rw [conv_comm]
      _ = (b + a) x := rfl
  zero := 0
  zero_add := by
    intro a
    apply Subtype.ext
    ext x
    calc (0 + a) x
      _ = (trivChar ‚àó a) x := rfl
      _ = a x := by rw [trivChar_conv]
  add_zero := by
    intro a
    apply Subtype.ext
    ext x
    calc (a + 0) x
      _ = (a ‚àó trivChar) x := rfl
      _ = a x := by rw [conv_trivChar]
  nsmul := nsmulRec
}

lemma FinPMF.sub_val [Sub Œ±] : a - b = (a*b).apply (fun x => x.1-x.2) := rfl

lemma FinPMF.add_val [Add Œ±] : a + b = (a*b).apply (fun x => x.1+x.2) := rfl

lemma FinPMF.apply_mul (a : FinPMF Œ±) (b : FinPMF Œ≤) (f : Œ± ‚Üí Œ≥) (g : Œ≤ ‚Üí Œ≥‚ÇÇ) [Nonempty Œ≥] [Fintype Œ≥] [DecidableEq Œ≥]
    [Nonempty Œ≥‚ÇÇ] [Fintype Œ≥‚ÇÇ] [DecidableEq Œ≥‚ÇÇ]:
    a.apply f * b.apply g = (a*b).apply (fun x => (f x.1, g x.2)) := by
  apply Subtype.ext
  ext x
  have ‚ü®x1, x2‚ü© := x
  change (a.apply f * b.apply g) (x1, x2) = (a*b).apply _ _
  rw [FinPMF.mul_val]
  apply Eq.symm
  convert_to ‚àë y ‚àà univ.filter (fun y => (f y.1, g y.2) = (x1, x2)), (a*b) y = _
  unfold apply transfer
  simp only [filter_congr_decidable]
  calc ‚àë y ‚àà univ.filter (fun y => (f y.1, g y.2) = (x1, x2)), (a*b) y
    _ = ‚àë y ‚àà univ.filter (fun y => f y.1 = x1 ‚àß g y.2 = x2), (a*b) y := by simp
    _ = ‚àë y ‚àà (univ √óÀ¢ univ).filter (fun y => f y.1 = x1 ‚àß g y.2 = x2), (a*b) y := by simp
    _ = ‚àë y ‚àà (univ.filter (fun y => f y = x1)) √óÀ¢ (univ.filter (fun y => g y = x2)), (a*b) y := by
      congr
      exact Finset.filter_product (fun y => f y = x1) (fun y => g y = x2)
    _ = ‚àë y1 ‚àà univ.filter (fun y => f y = x1), ‚àë y2 ‚àà univ.filter (fun y => g y = x2), (a*b) (y1, y2) := by
      rw [Finset.sum_product]
    _ = ‚àë y1 ‚àà univ.filter (fun y => f y = x1), ‚àë y2 ‚àà univ.filter (fun y => g y = x2), a y1 * b y2 := rfl
    _ = (‚àë y ‚àà univ.filter (fun y => f y = x1), a y) * (‚àë y ‚àà univ.filter (fun y => g y = x2), b y) := by
      rw [sum_mul_sum]

lemma FinPMF.apply_add (a : FinPMF Œ±) (b : FinPMF Œ≤) (f : Œ± ‚Üí Œ≥) (g : Œ≤ ‚Üí Œ≥) [Nonempty Œ≥] [Fintype Œ≥] [Add Œ≥] [DecidableEq Œ≥]:
    a.apply f + b.apply g = (a*b).apply (fun x => f x.1 + g x.2) := by
  apply Subtype.ext
  ext x
  change (apply a f + apply b g) x = _
  rw [FinPMF.add_val, FinPMF.apply_mul, FinPMF.apply_apply]
  rfl

theorem apply_ne_zero (a : FinPMF Œ±) (f : Œ± ‚Üí Œ≤) (x : Œ≤)
    (h : a.apply f x ‚â† 0) : ‚àÉ i, x = f i := transfer_ne_zero f a x h

end monoid

section linear_combination

noncomputable def FinPMF.linear_combination (a : FinPMF Œ±) (f : Œ± ‚Üí FinPMF Œ≤) : FinPMF Œ≤ :=
  ‚ü®(fun x => ‚àë y ‚àà univ, (a y) * (f y x)), by
    constructor
    rw [sum_comm]
    simp [‚Üê mul_sum]
    intros
    apply sum_nonneg
    intros
    exact mul_nonneg (nonneg _) (nonneg _)
    ‚ü©

theorem linear_combination_linear_combination [Fintype Œ≥] (a : FinPMF Œ±) (f : Œ± ‚Üí FinPMF Œ≤) (g : Œ≤ ‚Üí FinPMF Œ≥):
  FinPMF.linear_combination (FinPMF.linear_combination a f) g =
  FinPMF.linear_combination a (fun x => FinPMF.linear_combination (f x) g) := by
  simp only [FinPMF.linear_combination]
  apply Subtype.ext
  simp only [FinPMF.val_apply]
  ext x
  simp only [sum_mul, mul_sum]
  rw [sum_comm]
  simp [mul_assoc]

theorem linear_combination_apply [Nonempty Œ≥] [Fintype Œ≥] [DecidableEq Œ≥] (a : FinPMF Œ±) (f : Œ± ‚Üí FinPMF Œ≤) (g : Œ≤ ‚Üí Œ≥) :
  (FinPMF.linear_combination a f).apply g = FinPMF.linear_combination a (fun x => (f x).apply g) := by
  unfold FinPMF.apply transfer FinPMF.linear_combination
  apply Subtype.ext
  simp only [FinPMF.val_apply]
  ext x
  simp only [mul_sum]
  rw [sum_comm]

theorem linear_combination_mul [Nonempty Œ±'] [Fintype Œ±'] [Nonempty Œ≤'] [Fintype Œ≤'] (a : FinPMF Œ±) (f : Œ± ‚Üí FinPMF Œ±')
    (b : FinPMF Œ≤) (g : Œ≤ ‚Üí FinPMF Œ≤') :
    (a.linear_combination f) * (b.linear_combination g) = (a * b).linear_combination (fun ‚ü®x, y‚ü© => (f x) * (g y)) := by
  unfold FinPMF.linear_combination
  simp only [FinPMF.val_apply, instMulFinPMF]
  apply Subtype.ext
  simp only
  ext x
  simp [sum_mul_sum]
  apply sum_congr
  rfl
  intros
  apply sum_congr
  rfl
  intros
  ring

noncomputable def FinPMF.adjust (a : FinPMF Œ±) (x : Œ±) (p : ‚Ñù) (h‚ÇÅ : 0 ‚â§ p) (h‚ÇÇ : p ‚â§ 1) : FinPMF Œ± :=
  FinPMF.linear_combination (Œ± := Fin 2) ‚ü®![1-p, p], by
    constructor
    simp
    rw [Fin.forall_fin_two]
    simp [h‚ÇÅ, h‚ÇÇ]
  ‚ü© (![a, Uniform_singleton x] : Fin 2 ‚Üí FinPMF Œ±)

end linear_combination

section high_entropy

noncomputable def close_high_entropy [Fintype Œ±] (n : ‚Ñù) (Œµ : ‚Ñù) (a : FinPMF Œ±) : Prop :=
  ‚àÄ (H : Finset Œ±), (H.card ‚â§ n) ‚Üí ‚àë v ‚àà H, a v ‚â§ Œµ

lemma close_high_entropy_of_floor [Fintype Œ±] (n : ‚Ñù) (Œµ : ‚Ñù) (a : FinPMF Œ±)
    (h : close_high_entropy ‚åän‚åã‚Çä Œµ a):
    close_high_entropy n Œµ a := by
  intro H hH
  apply h
  simp only [Nat.cast_le]
  rw [Nat.le_floor_iff]
  exact hH
  refine LE.le.trans ?_ hH
  simp

lemma close_high_entropy_of_le [Fintype Œ±] (n : ‚Ñù) (Œµ‚ÇÅ Œµ‚ÇÇ : ‚Ñù) (hŒµ : Œµ‚ÇÅ ‚â§ Œµ‚ÇÇ) (a : FinPMF Œ±)
    (h : close_high_entropy n Œµ‚ÇÅ a):
    close_high_entropy n Œµ‚ÇÇ a := by
  intro H hH
  apply (h H hH).trans hŒµ

lemma close_high_entropy_apply_equiv [Fintype Œ±] [Nonempty Œ±] [Fintype Œ≤] [Nonempty Œ≤]
    (n Œµ : ‚Ñù) (a : FinPMF Œ±)
    (h : close_high_entropy n Œµ a) (e : Œ± ‚âÉ Œ≤) :
    close_high_entropy n Œµ (a.apply e) := by
  intro H hX
  simp_rw [FinPMF.apply_equiv]
  convert_to ‚àë x ‚àà H.map e.symm, a x ‚â§ Œµ
  simp
  apply h
  simp [hX]

lemma add_close_high_entropy [Fintype Œ±] [Nonempty Œ±] [AddCommGroup Œ±]
    (n Œµ : ‚Ñù) (a b : FinPMF Œ±)
    (h : close_high_entropy n Œµ a) :
    close_high_entropy n Œµ (a+b) := by
  intro H hX
  change ‚àë v ‚àà H, ‚àë x ‚àà univ.filter (fun (x : Œ± √ó Œ±) => x.1 + x.2 = v), a x.1 * b x.2 ‚â§ Œµ
  convert_to ‚àë v ‚àà H, ‚àë x, a (v - x) * b x ‚â§ Œµ
  congr with v
  apply sum_nbij' (fun x => x.2) (fun x => (v-x, x))
  ¬∑ simp
  ¬∑ simp
  ¬∑ simp only [mem_filter, mem_univ, true_and, Prod.forall, Prod.mk.injEq, and_true]
    intros _ _ h
    rw [‚Üê h]
    abel
  ¬∑ simp
  ¬∑ simp only [mem_filter, mem_univ, true_and, mul_eq_mul_right_iff, Prod.forall]
    intros _ _ h
    left
    rw [‚Üê h]
    congr
    abel
  rw [sum_comm]
  simp_rw [‚Üê sum_mul, mul_comm]
  convert_to ‚àë x, b x * ‚àë i ‚àà H.image (fun y => y - x), a i ‚â§ Œµ
  congr with x
  congr 1
  apply Eq.symm
  apply sum_image
  ¬∑ intros _ _ _ _ h
    exact sub_left_inj.mp h
  trans ‚àë x, b x * Œµ
  gcongr
  simp
  apply h
  trans (H.card : ‚Ñù)
  exact_mod_cast card_image_le
  exact hX
  simp [‚Üê sum_mul]



lemma close_high_entropy_linear_combination [Fintype Œ±] [Fintype Œ≤] [DecidableEq Œ≤] (n : ‚Ñù) (Œµ : ‚Ñù) (a : FinPMF Œ≤)
    (g : Œ≤ ‚Üí FinPMF Œ±) (h : ‚àÄ x, 0 < a x ‚Üí close_high_entropy n Œµ (g x)) :
  close_high_entropy n Œµ (a.linear_combination g) := by
  intro H hH
  unfold FinPMF.linear_combination
  change ‚àë v ‚àà H, ‚àë y, a y * (g y) v ‚â§ Œµ
  rw [sum_comm]
  simp_rw [‚Üê mul_sum]
  calc ‚àë x, a x * ‚àë i ‚àà H, (g x) i
    _ = ‚àë x ‚àà univ.filter (fun x => 0 < a x), a x * ‚àë i ‚àà H, (g x) i := by
      apply Eq.symm
      apply sum_subset_zero_on_sdiff
      simp
      intro x hx
      simp at hx
      have : 0 ‚â§ a x := by simp
      have : 0 = a x := by linarith
      rw [‚Üê this]
      simp
      simp
    _ ‚â§ ‚àë x ‚àà univ.filter (fun x => 0 < a x), a x * Œµ := by
      gcongr with i hi
      simp
      simp only [mem_filter, mem_univ, true_and] at hi
      apply h i hi _ hH
    _ = ‚àë x, a x * Œµ := by
      apply sum_subset_zero_on_sdiff
      simp
      intro x hx
      simp at hx
      have : 0 ‚â§ a x := by simp
      have : 0 = a x := by linarith
      rw [‚Üê this]
      simp
      simp
    _ = Œµ := by simp [‚Üê sum_mul]

end high_entropy

lemma filter_neg_le_inv_card_le (a : FinPMF Œ±) (n : ‚Ñù) (hn : 0 < n) :
    (univ.filter fun x => ¬¨a x ‚â§ 1/n).card ‚â§ n := calc ((univ.filter fun x => ¬¨a x ‚â§ 1/n).card : ‚Ñù)
  _ = (‚àë x ‚àà univ.filter (fun x => ¬¨a x ‚â§ 1/n), 1 / n) * n := by field_simp
  _ ‚â§ (‚àë x ‚àà univ.filter (fun x => ¬¨a x ‚â§ 1/n), a x) * n := by
    gcongr
    simp_all [le_of_lt]
  _ ‚â§ (‚àë x, a x) * n := by
    gcongr
    apply sum_le_sum_of_subset_of_nonneg
    simp
    intros
    simp
  _ = n := by simp
